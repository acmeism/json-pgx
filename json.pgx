json: <map> | <seq>

node: <map> | <seq> | <scalar>

map:
    / ~ <LCURLY> ~ /
    <pair>* % / ~ <COMMA> ~ /
    / ~ <RCURLY> ~ /

pair:
    <string>
    / ~ <COLON> ~ /
    <node>

seq:
    / ~ <LSQUARE> ~ /
    <node>* % / ~ <COMMA> ~ /
    / ~ <RSQUARE> ~ /

scalar:
    <string> |
    <number> |
    <boolean> |
    <null>

# Need to doublecheck string rules.
string: /
    <DOUBLE>
        ((?:
            <BACK><BACK> |
            <BACK><DOUBLE> |
            [^ <DOUBLE> <BREAK> ]
        )*)
    <DOUBLE>
/

# Need to check all possible allowable number forms: -.2 -0 -1. 1e9 etc
number: /(
    <DASH>?
    (?: 0 | [1-9]<DIGIT>* )
    (?: <DOT> <DIGIT>* )?
    (?: [eE] [<DASH><PLUS>]? <DIGIT>+ )?
)/

boolean: <true> | <false>

true: /true/

false: /false/

null: /null/
